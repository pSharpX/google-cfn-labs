plugins {
	id 'java'
	id 'application'
	id 'com.gradleup.shadow' version '8.3.5'
	id 'org.liquibase.gradle' version '2.2.0'
}

group = 'com.onebank.taskmaster.controlplane'
version = '0.0.1-SNAPSHOT'
def appName = "searchtask"

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

configurations {
	invoker
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	liquibaseRuntime 'org.liquibase:liquibase-core:4.8.0'
	liquibaseRuntime 'org.liquibase:liquibase-groovy-dsl:3.0.2'
	liquibaseRuntime 'info.picocli:picocli:4.7.4'
	liquibaseRuntime 'org.yaml:snakeyaml:1.19'
	liquibaseRuntime 'org.postgresql:postgresql:42.7.3'

	implementation ('com.google.inject:guice:7.0.0') {
		exclude group: 'com.google.guava', module: 'guava'
	}
	implementation group: 'com.google.guava', name: 'guava', version: '33.3.1-jre'
	implementation 'com.google.cloud.functions:functions-framework-api:1.1.0'
	invoker 'com.google.cloud.functions.invoker:java-function-invoker:1.3.0'
	compileOnly 'org.projectlombok:lombok:1.18.34'
	annotationProcessor 'org.projectlombok:lombok:1.18.34'
	implementation 'com.fasterxml.jackson.core:jackson-databind:2.17.2'
	implementation 'org.slf4j:slf4j-api:2.0.9'
	runtimeOnly 'ch.qos.logback:logback-classic:1.4.12'
	implementation 'org.hibernate.validator:hibernate-validator:8.0.0.Final'
	implementation 'org.glassfish.expressly:expressly:5.0.0'
	implementation 'org.mybatis:mybatis:3.5.6'
	implementation 'org.postgresql:postgresql:42.7.3'

	testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
	testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

application {
	mainClass = 'com.onebank.taskmaster.searchtask.ControlPlaneApplication'
}

tasks.named('test') {
	useJUnitPlatform()
}

shadowJar {
	mergeServiceFiles()
}

tasks.register("runFunction", JavaExec) {
	main = 'com.google.cloud.functions.invoker.runner.Invoker'
	classpath(configurations.invoker)
	inputs.files(configurations.runtimeClasspath, sourceSets.main.output)
	args(
			'--target', project.findProperty('run.functionTarget'),
			'--port', project.findProperty('run.port') ?: 8080
	)
	doFirst {
		args('--classpath', files(configurations.runtimeClasspath, sourceSets.main.output).asPath)
	}
}

liquibase {
	activities {
		main {
			changelogFile 'src/main/resources/db/changelog/db.changelog-1.0.yaml'
			url project.findProperty('liquibase.url')
			username project.findProperty('liquibase.username')
			password project.findProperty('liquibase.password')
			logLevel "info"
		}
		runList = 'main'
	}
}